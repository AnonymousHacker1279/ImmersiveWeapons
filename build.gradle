plugins {
	id 'eclipse'
	id 'maven-publish'
	id 'net.minecraftforge.gradle' version '5.+'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

repositories {
	maven {
		name "Progwml6 Maven"
		url "https://dvs1.progwml6.com/files/maven/"
	}
	maven {
		name "ModMaven"
		url "https://modmaven.dev/"
	}
	maven {
		name "Maven Central"
		url "https://repo.maven.apache.org/maven2"
	}
	maven {
		name "Bai Maven"
		url "https://maven.bai.lol"
	}
	maven {
		url 'https://maven.blamejared.com'
	}
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
	mappings channel: mappings_channel, version: mappings_version

	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'

			// For Patchouli to work in-dev
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

			jvmArgs '-Xmx4G', '-XX:+UseShenandoahGC'

			mods {
				immersiveweapons {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'

			// For Patchouli to work in-dev
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

			jvmArgs '-Xmx4G', '-XX:+UseShenandoahGC'

			mods {
				immersiveweapons {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'

			// For Patchouli to work in-dev
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

			args '--mod', 'immersiveweapons', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

			jvmArgs '-Xmx4G', '-XX:+UseShenandoahGC'

			mods {
				immersiveweapons {
					source sourceSets.main
				}
			}
		}
	}
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
	implementation 'org.jetbrains:annotations:23.0.0'

	minecraft "net.minecraftforge:forge:${forge_version}"

	// TODO: Re-enable as dependencies upgrade
	// Include JEI
	// implementation fg.deobf("mezz.jei:jei-${jei_version}")

	// Include WTHIT and badpackets
	compileOnly fg.deobf("mcp.mobius.waila:wthit-api:forge-${wthit_version}")
	runtimeOnly fg.deobf("mcp.mobius.waila:wthit:forge-${wthit_version}")
	implementation fg.deobf("lol.bai:badpackets:forge-${badpackets_version}")

	// Include Patchouli
	// compileOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}:api")
	// runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")

	// Include TerraBlender
	implementation fg.deobf("com.github.glitchfiend:TerraBlender-forge:${terrablender_version}")
}

// This block of code expands all the gradle properties in the specified resource targets.
// It copies them into the targets and expands all the defined properties.
def resourceTargets = ['data/immersiveweapons/patchouli_books/encyclopedia/book.json',
                       'data/immersiveweapons/patchouli_books/lorebook/book.json']
def intoTargets = ["$rootDir/out/production/resources/", "$rootDir/out/production/${project.name}.main/", "$rootDir/bin/main/"]
def replaceProperties = [build_version: version]
processResources {
	inputs.properties replaceProperties
	replaceProperties.put 'project', project

	filesMatching(resourceTargets) {
		expand replaceProperties
	}

	intoTargets.each { target ->
		if (file(target).exists()) {
			copy {
				from(sourceSets.main.resources) {
					include resourceTargets
					expand replaceProperties
				}
				into target
			}
		}
	}
}

// Add properties to JAR manifest
jar {
	manifest {
		attributes([
				"Specification-Title"     : "immersiveweapons",
				"Specification-Vendor"    : "anonymoushacker1279",
				"Specification-Version"   : "1", // We are version 1 of ourselves
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : "${version}",
				"Implementation-Vendor"   : "anonymoushacker1279",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

jar.finalizedBy('reobfJar')

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact jar
		}
	}
	repositories {
		repositories {
			maven {
				name "AnonymousHacker1279"
				url "https://maven.anonymoushacker1279.tech/releases"
				credentials(PasswordCredentials)
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}