plugins {
	id 'java-library'
	id 'maven-publish'
	id 'net.neoforged.gradle.userdev' version '7.0.165'
	id "com.hypherionmc.modutils.modpublisher" version "2.1.6"
	id 'idea'
}

apply from: 'optimization_utils.gradle'

repositories {
	mavenLocal()
	maven {
		name "AnonymousHacker1279"
		url "https://maven.anonymoushacker1279.tech/releases"
	}
	maven {
		name "Jared's Maven"
		url 'https://maven.blamejared.com'
	}
	maven {
		name "ModMaven"
		url "https://modmaven.dev/"
	}
	maven {
		name "Maven Central"
		url "https://repo.maven.apache.org/maven2"
	}
	maven {
		name "Bai Maven"
		url "https://maven2.bai.lol"
	}
	maven {
		name "AzureDoom's Maven"
		url 'https://libs.azuredoom.com:4443/mods'
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
}

version = mod_version
group = mod_group_id

base {
	archivesName = project.mod_id
}

jarJar.enable()

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println('Java: ' + System.getProperty('java.version') + ' | JVM: ' + System.getProperty('java.vm.version') + ' (' + System.getProperty('java.vendor') + ') | Arch: ' + System.getProperty('os.arch'))

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

sourceSets {
	main {
		resources {
			// Include resources generated by data generators.
			srcDirs += "src/generated/resources"
		}
	}
	generated

	// Separate datagen sourceSet from main to shrink the build jar
	datagen {
		compileClasspath += main.output
	}
}

// Ensure other source sets receive the necessary dependencies
configurations {
	extendConfigurations(implementation, testImplementation, datagenImplementation)
	extendConfigurations(compileOnly, testCompileOnly, datagenCompileOnly)
	extendConfigurations(runtimeOnly, testRuntimeOnly, datagenRuntimeOnly)
}

static void extendConfigurations(Configuration base, Configuration... configurations) {
	for (configuration in configurations) {
		configuration.extendsFrom(base)
	}
}

runs {
	configureEach {
		systemProperty 'forge.logging.markers', 'REGISTRIES'
		systemProperty 'forge.logging.console.level', 'debug'

		jvmArguments.addAll '-Xmx6G'

		// If the selected toolchain is a JBR, enable DCEVM for improved hotswapping
		if (it.project.javaToolchains.launcherFor(java.toolchain).map { it.metadata.vendor }.getOrElse("").contains("JetBrains")) {
			jvmArguments.addAll '-XX:+AllowEnhancedClassRedefinition', '-XX:NonProfiledCodeHeapSize=256M', '-XX:ReservedCodeCacheSize=300M'
		}

		modSources project.sourceSets.main, project.sourceSets.datagen
	}

	client {
		// Setup client information from gradle.properties in the user home
		// All of these are optional
		if (project.hasProperty('mc_uuid')) {
			arguments('--uuid', (String) project.property('mc_uuid'))
		}
		if (project.hasProperty('mc_username')) {
			arguments('--username', (String) project.property('mc_username'))
		}
		if (project.hasProperty('mc_accessToken')) {
			arguments('--accessToken', (String) project.property('mc_accessToken'))
		}
	}

	server {
		argument '--nogui'
	}

	data {
		arguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
	}
}

// Include resources generated by data generators
sourceSets.main.resources {
	srcDir 'src/generated/resources'
	// Exclude the cache directory
	exclude '.cache'
}

dependencies {
	implementation 'org.jetbrains:annotations:24.1.0'

	implementation "net.neoforged:neoforge:${neoforge_version}"

	implementation(jarJar("dev.lukebemish:biomesquisher:[${biomesquisher_version}]")) {
		version {
			prefer biomesquisher_version
		}
		capabilities {
			requireCapability("dev.lukebemish:biomesquisher-neoforge")
		}
	}

	implementation(jarJar("net.kyrptonaught.customportalapi:cpapireforged-neo-${minecraft_version}:[${custom_portals_api_version},)")) {
		version {
			prefer custom_portals_api_version
		}
	}

	// Include Immersive Weapons Compatibility Bridge (IWCB)
	implementation "tech.anonymoushacker1279.iwcompatbridge:IWCompatBridge:${iwcb_version}"

	// Include JEI
	runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
	// Include WTHIT and badpackets
	runtimeOnly "mcp.mobius.waila:wthit:neo-${wthit_version}"
	runtimeOnly "lol.bai:badpackets:neo-${badpackets_version}"
}

tasks.named('processResources', ProcessResources).configure {
	LinkedHashMap<String, String> replaceProperties = [
			minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
			neoforge_version    : neoforge_version, neoforge_version_range: neoforge_version_range,
			loader_version_range: loader_version_range, mod_version: mod_version
	]
	inputs.properties replaceProperties

	filesMatching(['META-INF/neoforge.mods.toml']) {
		expand replaceProperties + [project: project]
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

java {
	withSourcesJar()
	withJavadocJar()
}

tasks.named('sourcesJar', Jar).configure {
	from sourceSets.main.allSource
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('jar', Jar).configure {
	archiveClassifier = "slim"
	manifest {
		attributes([
				'Specification-Title'     : mod_id,
				'Specification-Vendor'    : "AnonymousHacker1279",
				'Specification-Version'   : '1',
				'Implementation-Title'    : project.name,
				'Implementation-Version'  : project.jar.archiveVersion,
				'Implementation-Vendor'   : "AnonymousHacker1279",
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
				"MixinConfigs"            : "mixins.immersiveweapons.json"
		])
	}
}

tasks.jarJar.configure {
	archiveClassifier = ""
}

tasks.named('build').configure {
	dependsOn('jarJar')
}

artifacts {
	archives javadocJar
	archives sourcesJar
	archives tasks.jarJar
}

publishing {
	publications {
		register('mavenJava', MavenPublication) {
			artifact jar
			artifact tasks.jarJar
			artifact sourcesJar
			artifact javadocJar
		}
	}
	repositories {
		maven {
			name "AnonymousHacker1279"

			String baseMavenUrl = "https://maven.anonymoushacker1279.tech/"
			url mod_version.contains("SNAPSHOT") ? baseMavenUrl + "snapshots" : baseMavenUrl + "releases"

			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}

if (file("RELEASE_NOTES.md").exists()) {
	publisher {
		apiKeys {
			// Load API keys from tokens.properties file
			Properties tokens = new Properties()
			file("tokens.properties").withInputStream {
				stream -> tokens.load(stream)
			}

			curseforge = tokens.getProperty("curseforge")
			modrinth = tokens.getProperty("modrinth")
		}

		setDebug(false)
		setCurseID("494454")
		setModrinthID("QZFdnUQ5")
		setGithubRepo("AnonymousHacker1279/ImmersiveWeapons")
		setVersionType("release")
		// Read the contents of the RELEASE_NOTES.md file
		setChangelog(file("RELEASE_NOTES.md").text)
		// Get the IW version from the mod_version variable, it's the last part of the version string
		setVersion("v" + mod_version.split("-")[1])
		setDisplayName("Immersive Weapons - v" + mod_version.split("-")[1])
		setGameVersions([minecraft_version])
		setLoaders("neoforge")
		setCurseEnvironment("both")
		setArtifact(tasks.jarJar)
		setJavaVersions([JavaVersion.VERSION_21])
		addAdditionalFile(javadocJar, sourcesJar)

		curseDepends {
			optional "immersive-weapons-compatibility-bridge", "attributefix"
			embedded 'biomesquisher', 'custom-portal-api-reforged'
		}

		modrinthDepends {
			optional "immersive-weapons-compatibility-bridge", "attributefix"
			embedded 'biomesquisher', 'custom-portal-api-reforged'
		}
	}
} else {
	println("No RELEASE_NOTES.md found, publishing outside of Maven is disabled.")
}

tasks.named('javadoc', Javadoc).configure {
	options {
		tags = [
				'reason:a:Mixin Reason'
		]
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
	options.fork = true // Use a separate JVM process for Java compilation
}

idea {
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}